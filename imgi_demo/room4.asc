// room script file
bool can_go_next_room, submitted;
String buf, log;
ImGi_Result* tbx;

function room_RepExec()
{
  ImGi.Begin();
    
  if(ImGi.BeginWindow("Text Input", 170, 100, 320, 196, eImGi_Opt_AlignCenter | 
                                                        eImGi_Opt_NoClose ))
  {
    ImGi.LayoutRow1(290);  
    if(can_go_next_room){
      if(ImGi.Button("To  next  room!")) player.ChangeRoom(5, 241, 147, eDirectionDown);
    }    
    ImGi.Text("Try the text input below!");
    
    ImGi.LayoutRow1(290, 90);
    if(ImGi.BeginPanel("pan"))
    {
      ImGi.LayoutRow1(-1);
      ImGi.Text(log);
      ImGi.EndPanel();
    }           
    
    ImGi.LayoutRow2(220, 60);
    buf = ImGi.TextBox("input",buf, 200, tbx);
    if(submitted)
    {
      ImGi.SetFocusLastControl(); // this will keep the focus on the textbox after Enter pressed
      submitted = false; // need this to reset state
      
      log = log.Append(buf);
      log = log.Append("\n");
      buf = "";
      can_go_next_room = true;
    }
    
    // hiting enter or clicking submit should both add to the log!
    if(ImGi.Button("submit") || tbx.state == eImGi_Res_Submit) submitted = true;
  
    ImGi.EndWindow();
  }
    
  ImGi.End();  
}

function room_Load()
{
  buf = "";
  log = "";
  tbx = new ImGi_Result;
}