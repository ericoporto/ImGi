// room script file
bool can_go_next_room;
CheckBoxState* key_chk, cup_chk;
bool click1, click2, click3, click4;

function room_RepExec()
{
  ImGi.Begin();
    
  if(ImGi.BeginWindow("Images!", 
                      Screen.Width/2-280/2, Screen.Height/2-280/2, 
                      280, 280, 
                      eImGi_Opt_AlignCenter | eImGi_Opt_NoClose))
  { 
    
    if(click1 && click2 && click3 && click4) {
      ImGi.LayoutRow1(-1, 26); 
      if(ImGi.Button("Go to next room!",2054, eImGi_Opt_AlignCenter))
      {
        player.ChangeRoom(6, 241, 147, eDirectionDown);
      }
    } 
    
    ImGi.LayoutRow1(-1); //using negative numbers position relative to the right side.
    ImGi.Text("You can use ImGi.Image() for non interactable sprites.\n");
    
    ImGi.LayoutRow4(1, 1, 1, 1); // you can pass a smaller size than the image and let it resize!
    
    ImGi.Image(2103); 
    ImGi.Image(2107); 
    ImGi.Image(2111); 
    ImGi.Image(2115);
    
    ImGi.LayoutRow1(-1);
    ImGi.Text("You can also use ImGi.ButtonImage() for fully interactable buttons with images!\n");
    
    ImGi.LayoutRow4(1, 1, 1, 1);
    
    if(ImGi.ButtonImage(null, 2100, 2101, 2102)) {click1 = true;}
    if(ImGi.ButtonImage(null, 2104, 2105, 2106)) {click2 = true;}
    if(ImGi.ButtonImage(null, 2108, 2109, 2110)) {click2 = true;}
    if(ImGi.ButtonImage(null, 2112, 2113, 2114)) {click1 = true;}
    
    ImGi.LayoutRow1(-1);
    ImGi.Text("But sometimes you just want a quick button with an image inside it...");
    ImGi.Text("Then you can use regular buttons, and pass a sprite in the icon part.");
    
    ImGi.LayoutRow2(100, -1);
    if(ImGi.Button("Button()", 2041)) {click3 = true;}
    ImGi.Text("...");
    
    ImGi.LayoutRow1(-1);
    ImGi.Text("The image will be forced in a square format when you do this, so it may not look great, but it's useful to quickly define a button.");
   
    ImGi.Text("Finally, when defining a checkbox, you can pass an icon to replace the checkbox one.\n This icon can be sprite!");
    
    ImGi.LayoutRow2(100, -1);
    if(ImGi.CheckBox("A key?",key_chk, 2042)) {click4 = true;}
    if(ImGi.CheckBox("A cup?",cup_chk, 2041)) {click4 = true;}
    
    ImGi.LayoutRow1(-1);
    ImGi.Text("Ah, the label in the checkbox is optional too, you can pass null if you don't want to name it");
    

    
    ImGi.EndWindow();
  }
    
  ImGi.End();  
}

function hGlowingOrb_AnyClick()
{

}

function room_Load()
{
  ImGi.Style = ImGi_Style.Create();
  ImGi.Style.font = eFontSpeechWOOutline;
  key_chk = new CheckBoxState;
  cup_chk = new CheckBoxState;
  key_chk.state = true;
  cup_chk.state = true;
}
